//Shotgun. Simple as it goes.
Actor "Shot-Gun" : Weapon
{
  Obituary "%o was crippled by %k's Shotgun."
  Weapon.KickBack 100
  Weapon.AmmoType1 "ZSecShellIn"
  Weapon.AmmoGive1 8
  Weapon.AmmoUse1 1
  Weapon.AmmoType2 "ZSecShell"
  Weapon.AmmoGive2 8
  Weapon.AmmoUse2 0
  Weapon.SlotNumber 2
  +AMMO_OPTIONAL
  +ALT_AMMO_OPTIONAL
  +INVENTORY.UNDROPPABLE
  +WEAPON.NOAUTOAIM
  -WEAPON.NOAUTOFIRE
  States
  {
  Ready:
    XSTG A 0 A_TakeInventory("WasReloading",1)
    XSTG A 1 A_WeaponReady
    Loop
  Deselect: 
    XSTG A 1 A_Lower
	XSTG A 0 A_Lower
    Loop
  Select:
    XSTG A 1 A_Raise
	XSTG A 0 A_Raise
    Loop
  Fire:
  	XSTG A 0 A_JumpIfInventory("WasReloading",1,"DoneReloading")
    XSTG A 0 A_JumpIfNoAmmo("Empty")
	XSTG A 0 A_PlaySoundEx("ZSecShotgun/Fire","auto",0)
    XSTG A 0 A_FireBullets(4.8,3.0,6,Random(4,5),"BulletPuff")
    TNT1 A 7 A_GunFlash
	XSTG A 10
    XSTG B 3
    XSTG C 3 A_PlaySound("ZSecShotgun/Cock")
	XSTG D 6
    XSTG E 3
    XSTG F 3
	XSTG A 10
    Goto Ready
   Empty:
    XSTG B 3
    XSTG C 3 A_PlaySound("ZSecShotgun/Cock")
	XSTG D 6
    XSTG E 3
    XSTG F 3
	XSTG A 3
    Goto AltFire
  OldEmpty:
    XSTG FFFFEEEE 1
	XSTG E 0 A_PlaySound("ZSecShotgun/Open")
	XSTG EEEEEE 1
	XSTG C 1 A_PlaySound("ZSecShotgun/Cock")
	XSTG CCCCCDDDDDDEE 1
	XSTG EEEEEE 1
	XSTG F 1 A_PlaySound("ZSecShotgun/Close")
	XSTG FFFF 1
	XSTG BBBB 1
	XSTG AAAA 1
	Goto Ready
  AltFire:
    XSTG A 0 A_JumpIfInventory("WasReloading",1,"DoneReloading")
	XSTG A 1
    XSTG A 0 A_JumpIfInventory("ZSecShellIn", 0, "Ready")
	XSTG FFFFEEEE 1
	XSTG E 0 A_PlaySound("ZSecShotgun/Open")
	XSTG EEEEEE 1
	XSTG I 0 A_JumpIfInventory("ZSecShellIn",8,2)
	XSTG I 0 A_JumpIfInventory("ZSecShell",1,"Reloading")
	XSTG I 0
	Goto CloseShotgun
  ReloadingPlus:
  	Goto Reloading+5
  Reloading:
  	XSTR A 0 A_GiveInventory("WasReloading",1)
	XSTR ABCDEEEEEEEEFGHIJKLM 1
	XSTR A 0 A_PlaySound("ZSecShotgun/Load")
	XSTR A 0 A_GiveInventory("ZSecShellIn",1)
	XSTR A 0 A_TakeInventory("ZSecShell",1)
	XSTR E 1 A_WeaponReady (WRF_NOBOB)
	XSTR A 0 A_JumpIfInventory("ZSecShellIn",8,"DoneReloading")
	XSTR A 0 A_JumpIfInventory("ZSecShell",1,"ReloadingPlus")
	XSTR A 0
	Goto CloseShotgun
  DoneReloading:
  	XSTR E 0 A_TakeInventory("WasReloading",1)
  	XSTR EEEEEEDCBA 1
  CloseShotgun:
    XSTG C 0 A_TakeInventory("WasReloading",1)
	XSTG C 3 A_PlaySound("ZSecShotgun/Cock")
	XSTG D 6
	XSTG E 3
	XSTG F 3 A_PlaySound("ZSecShotgun/Close")
	XSTG A 17
	Goto Ready
  Flash:
    XSTF A 4 bright
    XSTF B 3 bright
    XSTF B 0 bright
	Stop
  Spawn:
    TNT1 A 0
    Stop
  }
}

Actor WasReloading : Inventory
{
Inventory.MaxAmount 1
Inventory.Amount 0
}

Actor ZSecShell : Ammo
{
  +INVENTORY.IGNORESKILL
  Inventory.PickupMessage "If you can read this, you're probably either in the wad or a cheating faggot."
  Inventory.Amount 4
  Inventory.MaxAmount 48
  Inventory.Icon "SHELA0"
  Ammo.BackPackAmount 4
  Ammo.BackPackMaxAmount 48
  States
  {
  Spawn:
    TNT1 A 0
    Stop
  }
}

Actor ZSecShellIn : Ammo
{
   +INVENTORY.IGNORESKILL
   Inventory.MaxAmount 8
   Inventory.Icon "SHELA0"
   States
   {
   Spawn:
      TNT1 A 0
      Stop
   }
}
