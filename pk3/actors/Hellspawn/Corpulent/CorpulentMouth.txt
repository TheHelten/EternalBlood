//Corpulent's one and only weapon.
Actor "Corpulent Mouth" : Weapon
{
   Weapon.SelectionOrder 1
   Weapon.SlotNumber 1
   Inventory.PickupMessage "The boiling stomach of a corpulent..."
   Weapon.Kickback 100
   Obituary "%o was killed by %k's blood vomit."
   Weapon.AmmoType1 "VomitAmmo"
   Weapon.AmmoUse1 0
   Weapon.AmmoGive1 0
   Weapon.AmmoUse2 0
   Weapon.AmmoGive2 0
   +AMMO_OPTIONAL
   +ALT_AMMO_OPTIONAL
   +INVENTORY.UNDROPPABLE
   +NOAUTOFIRE
   States
   {
   Spawn:
      TNT1 A 0
   	  TNT1 A 0 //State checker.
      TNT1 A 0
      Stop
   Ready:
      TNT1 A 0 SetPlayerProperty(0,0,0)
   	  TNT1 A 0 A_TakeInventory("Main Fire",2)
	  TNT1 A 0 A_TakeInventory("Alt Fire",1)
      TNT1 A 1 A_WeaponReady
	  TNT1 A 0 A_GiveInventory("VomitAmmo",1)
      Loop
   Deselect:
	  TNT1 A 1 A_Lower
      Loop
   Select:
	  TNT1 A 1 A_Raise
      Loop
   Fire:
      TNT1 A 0 A_JumpIfHealthLower(101,"WeakVomitCheck")
	  TNT1 A 1 A_JumpIfInventory("VomitAmmo",50,"Vomit")
	  TNT1 A 0 A_GiveInventory("VomitAmmo",1)
      Goto Ready
   NormalVomitCheck:
   	  TNT1 A 0 A_JumpIfInventory("VomitAmmo",100,"Vomit")
	  TNT1 A 1
	  TNT1 A 0
	  Goto Ready
   Vomit:
      TNT1 A 0 SetPlayerProperty(0,1,0)
      TNT1 A 0 A_GiveInventory("Main Fire",1)
	  TNT1 A 0 A_TakeInventory("VomitAmmo",50)
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Vomit","SoundSlot10")
	  TNT1 A 0 A_FireCustomMissile("BloodVomitSpine",0,0,0,18)
	  TNT1 AAAAA 2 A_FireCustomMissile("BloodVomitNormal",Random(-4,4),0,0,18)
	  TNT1 AAA 4 A_FireCustomMissile("BloodVomitNormal",Random(-4,4),0,0,18)
	  TNT1 AAAAAAAAAAAAAAA 1 //A full second, for good measure.
   	  Goto Ready
   WeakVomitCheck:
      TNT1 A 1 A_JumpIfInventory("VomitAmmo",50,"WeakVomit")
	  TNT1 A 0 A_GiveInventory("VomitAmmo",1)
	  TNT1 A 0
   	  Goto Ready
   WeakVomit:
   	  TNT1 A 0 SetPlayerProperty(0,1,0)
      TNT1 A 0 A_GiveInventory("Main Fire",2)
	  TNT1 A 0 A_TakeInventory("VomitAmmo",50)
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Vomit","SoundSlot10")
	  TNT1 A 0 A_FireCustomMissile("BloodVomitSpine",0,0,0,18) //Even if health is reduced, same power.
	  TNT1 AAAAA 3 A_FireCustomMissile("BloodVomitWeak",Random(-3,3),0,0,17) //Mini effects are nice.
	  TNT1 AAA 5 A_FireCustomMissile("BloodVomitWeak",Random(-3,3),0,0,17)
	  TNT1 AAAAAAAAAA 1 //This amounts to a full second +5 tics, but the vomitting is slower too.
   	  Goto Ready
   AltFire:
   	  TNT1 A 0 A_JumpIfHealthLower(101,"NormalVomitCheck")
	  TNT1 A 1 A_JumpIfInventory("VomitAmmo",100,"VomitCannon")
	  TNT1 A 0 A_GiveInventory("VomitAmmo",1)
	  Goto Ready
   VomitCannon:
	  TNT1 AAAAA 1
	  TNT1 A 0 A_GiveInventory("Alt Fire",1)
	  TNT1 A 0 SetPlayerProperty(0,1,0)
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Choking","SoundSlot10")
	  TNT1 AAAAAAAAAA 1
	  TNT1 AAAAA 1
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Choking","SoundSlot10")
	  TNT1 AAAAAAAAAA 1
	  TNT1 AAAAA 1
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Choking","SoundSlot10")
	  TNT1 AAAAAAAAAA 1
	  TNT1 AAAAA 1
	  TNT1 A 0 A_TakeInventory("VomitAmmo",100)
	  TNT1 A 0 A_PlaySoundEx("Corpulent/Vomit","SoundSlot10")
	  TNT1 A 0 A_FireCustomMissile("BloodVomitSpine",0,0,0,18)
	  TNT1 A 0 A_FireCustomMissile("BloodCannonVomit",0,0,0,18)
	  TNT1 A 0 A_TakeInventory("Alt Fire",1)
	  TNT1 A 0 SetPlayerProperty(0,0,0)
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1 //Run around, but no more vomitting for a bit.
	  TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 1
   	  Goto Ready
   }
}

//Vomit Ammo
Actor VomitAmmo : Ammo
{
	Inventory.MaxAmount 100
	Ammo.BackPackMaxAmount 100
	+INVENTORY.IGNORESKILL
}

//The Projectiles
Actor BloodVomitNormal
{
Radius 2
Height 4
Damage (5)
Speed 20
Scale 0.80
SeeSound "Misc/XDeath1"
DeathSound "Corpulent/VomitHit"
PROJECTILE
-EXTREMEDEATH
+FORCEXYBILLBOARD
+NOTIMEFREEZE
-NOGRAVITY
States
{
Spawn:
FGOR A 0
FGOR A 0 ThrustThingZ(0,Random(0,2),0,1)
FGOR ABCDEF 3 A_SpawnItemEx("FallBloodNormal",0,0,0,Random(-1,1),Random(-1,1),Random(-1,1),0,0,10)
Goto Spawn+2
Death:
FGOR GHIJ 5
Stop
}
}

Actor BloodVomitWeak
{
Radius 1
Height 2
Damage (3)
Speed 10
Scale 0.40
SeeSound "Misc/XDeath1"
DeathSound "Corpulent/VomitHit"
PROJECTILE
-EXTREMEDEATH
+FORCEXYBILLBOARD
+NOTIMEFREEZE
-NOGRAVITY
States
{
Spawn:
FGOR A 0
FGOR A 0 ThrustThingZ(0,Random(0,1),0,1)
FGOR ABCDEF 3 A_SpawnItemEx("FallBloodWeak",0,0,0,Random(-1,1),Random(-1,1),Random(-1,1),0,0,10)
Goto Spawn+2
Death:
FGOR GHIJ 5
Stop
}
}

Actor BloodVomitSpine //A very short range projectile from the mouth... very high damage rate.
{
Radius 2
Height 4
Damage (10)
Speed 6
Scale 1.00
SeeSound "Corpulent/VomitFire"
PROJECTILE
-EXTREMEDEATH
+RIPPER //Uhn tiss.
+NOGRAVITY
+FORCEXYBILLBOARD
+NOTIMEFREEZE
States
{
Spawn:
TNT1 A 0
TNT1 AAAAAAAAAAAA 1 A_SpawnItemEx("SpineBlood",0,0,0,Random(-1,1),Random(-1,1),Random(-1,1),0,0,10)
Goto Death
Death:
TNT1 A 0
TNT1 A 1
TNT1 A 0
Stop
}
}

Actor BloodCannonVomit
{
Radius 4
Height 8
Damage (50)
Speed 30
Scale 1.20
SeeSound "Misc/XDeath1"
DeathSound "Corpulent/VomitHit"
PROJECTILE
-EXTREMEDEATH
+NOGRAVITY
+FORCEXYBILLBOARD
+NOTIMEFREEZE
States
{
Spawn:
FGOR A 0
FGOR A 0 ThrustThingZ(0,Random(0,2),0,1)
FGOR ABCDEF 1 A_SpawnItemEx("SpineBlood",0,0,0,Random(-1,1),Random(-1,1),Random(-1,1),0,0,10)
Goto Spawn+2
Death:
FGOR GGGGGGGHHHHHHHIIIIIIIJJJJJJJ 1 A_SpawnItemEx("XBlood",0,0,4) //Only 4 Z so it can spawn and fly away.
Stop
}
}

Actor SpineBlood
{
    Radius 1
    Height 1
	Alpha 0.9
	Speed 2
	RenderStyle Translucent
	Scale 0.2
	Gravity 0.02
    +NOTELEPORT
	+NOBLOCKMAP
	+MISSILE
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+LOWGRAVITY
	-NOGRAVITY
    States
    {
    Spawn:
        BLOD A 0
        Goto Death
	Death:
	    BLOD AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.06)
		Stop
    }
}

Actor FallBloodNormal
{
    Radius 1
    Height 1
	Alpha 0.3
	Speed 2
	RenderStyle Translucent
	Scale 0.2
	Gravity 0.02
    +NOTELEPORT
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+MISSILE
	-NOGRAVITY
	-LOWGRAVITY
    States
    {
    Spawn:
        BLOD A 0
        Goto Death
	Death:
	    BLOD AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.02)
		Stop
    }
}

Actor FallBloodWeak
{
    Radius 1
    Height 1
	Alpha 0.3
	Speed 1
	RenderStyle Translucent
	Scale 0.1
	Gravity 0.02
    +NOTELEPORT
	+NOBLOCKMAP
	+FORCEXYBILLBOARD
	+NOTIMEFREEZE
	+MISSILE
	-NOGRAVITY
	-LOWGRAVITY
    States
    {
    Spawn:
        BLOD A 0
        Goto Death
	Death:
	    BLOD AAAAAAAAAAAAAAAAAAAA 1 A_FadeOut(0.02)
		Stop
    }
}
